# Maintainer: Vardan Gevorgyan  <vgevorgyan@vtgsoftware.com>
# Maintainer: Chris Speck  <chris.speck(at)annalise(dot)ai>

pkgname=slack-desktop-wayland
_original_pkgname=slack-desktop
pkgver=4.46.101
pkgrel=1
pkgdesc="Slack Desktop (Beta) for Linux with Wayland Support"
arch=('x86_64')
url="https://slack.com/downloads"
license=('custom')
depends=('gtk3' 'libsecret' 'libxss' 'nss' 'pipewire' 'xdg-utils' 'xdg-desktop-portal')
optdepends=(
  'libappindicator-gtk3: Systray indicator support'
  'org.freedesktop.secrets: Keyring password store support'
  'xdg-desktop-portal-gnome: xdg-desktop-portal support for GNOME'
  'xdg-desktop-portal-gtk: xdg-desktop-portal support for GNOME and GTK'
  'xdg-desktop-portal-kde: xdg-desktop-portal support for KDE'
  'xdg-desktop-portal-lxqt: xdg-desktop-portal support for LXQt'
  'xdg-desktop-portal-hyprland: xdg-desktop-portal support for Hyprland'
  'xdg-desktop-portal-wlr: xdg-desktop-portal support for wlroots-based Wayland compositors'
)
source=(
  "https://downloads.slack-edge.com/desktop-releases/linux/x64/${pkgver}/${_original_pkgname}-${pkgver}-amd64.deb"
  "${_original_pkgname}.patch"
)

noextract=("${_original_pkgname}-${pkgver}-amd64.deb")
b2sums=(
  '8e6ae1a88bfb832c46ef7bf1878457f861cc1cbfba595f9e7553c82659d11188fc43ab6f55889bbcfc019f522ad6742444517aad35e334c847e570b2915331d0'
  '0e5318977aeb8033ec90495008f4d03b003f02fdd4bf764e8963e5b886169b87b65fac6b1b504100c27a30be1d4b923ef2b0212f6e73760737fb9d554f3df6ed'
)
provides=('slack-desktop')
conflicts=('slack-desktop' 'slack-electron')

package() {
  bsdtar -O -xf "slack-desktop-${pkgver}"*.deb data.tar.xz | bsdtar -C "${pkgdir}" -xJf -

  # Fix hardcoded icon path in .desktop file
  patch -d "${pkgdir}" -p1 <"${_original_pkgname}".patch

  # Permission fix
  find "${pkgdir}" -type d -exec chmod 755 {} +

  # Remove all unnecessary stuff
  rm -rf "${pkgdir}/etc"
  rm -rf "${pkgdir}/usr/lib/slack/src"
  rm -rf "${pkgdir}/usr/share/lintian"
  rm -rf "${pkgdir}/usr/share/doc"

  # Move license
  install -dm755 "${pkgdir}/usr/share/licenses/${_original_pkgname}"
  mv "${pkgdir}/usr/lib/slack/LICENSE" "${pkgdir}/usr/share/licenses/${_original_pkgname}"
  ln -s "/usr/share/licenses/${_original_pkgname}/LICENSE" "${pkgdir}/usr/lib/slack/LICENSE"

  # patch the asar file to fix/enable pipewire
  # see https://github.com/flathub/com.slack.Slack/issues/101#issuecomment-1807073763
  sed -i -e 's/,"WebRTCPipeWireCapturer"/,"_ebRTCPipeWireCapturer"/' "${pkgdir}/usr/lib/slack/resources/app.asar"
}
