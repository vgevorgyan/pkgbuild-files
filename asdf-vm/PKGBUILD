# Maintainer: Vardan Gevorgyan <vgevorgyan@vtgsoftware.com>
# Maintainer: Alec Mev <alec@mev.earth>
# Maintainer: Raphael Nestler <raphael.nestler@gmail.com>
# Maintainer: Juliette Cordor <me@cordor.dev>

_pkgname=asdf
pkgname=asdf-vm
pkgver=0.18.0
pkgrel=3
pkgdesc='Extendable version manager with support for Ruby, Node.js, Elixir, Erlang & more'
arch=('x86_64')
url='https://asdf-vm.com'
license=('MIT')
depends=(
  'curl'
  'git'
)
optdepends=(
  'autoconf'
  'automake'
  'bash-completion: For completions to work in Bash'
  'libffi'
  'libtool'
  'libxslt'
  'libyaml'
  'ncurses'
  'openssl'
  'readline'
  'unixodbc'
  'unzip: Needed by some plugins, like Elixir'
)
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/asdf-vm/asdf/archive/v${pkgver}.tar.gz"
  "https://github.com/asdf-vm/asdf/releases/download/v${pkgver}/asdf-v${pkgver}-linux-amd64.tar.gz"
)
sha256sums=(
  'c452a886503277260b7c82c07031800340f892bb2147bb3af67dca7268a410b5'
  '4d3007070166cb0a652af26c3f0462b021e04cb26c4ab13894d13689da89f5b8'
)

package() {
  install -Dm755 asdf "$pkgdir"/usr/bin/$_pkgname

  cd "${_pkgname}-${pkgver}"

  local usrshare="${pkgdir}/usr/share"

  local docdir="${usrshare}/doc/${pkgname}"
  mkdir -p "${docdir}"
  cp help.txt "${docdir}"

  # https://aur.archlinux.org/packages/asdf-vm#comment-886293
  find . \
    -path ./.github \
    -prune \
    -o \
    -name '*.md' \
    -exec cp --parents '{}' "${docdir}" \;

  install -Dm644 -t "${usrshare}/licenses/${pkgname}/" LICENSE

  install -Dm644 internal/completions/asdf.bash "${usrshare}/bash-completion/completions/asdf"
  install -Dm644 internal/completions/asdf.fish "${usrshare}/fish/vendor_completions.d/asdf.fish"
  install -Dm644 internal/completions/asdf.zsh "${usrshare}/zsh/site-functions/_asdf"
}
